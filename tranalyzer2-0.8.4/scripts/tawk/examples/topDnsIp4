#!/usr/bin/env awk
#
# Returns the top N DNS answers IPv4 addresses
#
# Dependencies:
#   - dnsDecode
#
# Examples:
#   - tawk 'topDnsIp4()' file.txt
#   - tawk 'topDnsIp4(5)' file.txt

@include "aggr"
@include "hdr"
@include "port"
@include "strisempty"
@include "_validate_col"

function topDnsIp4(n,        _i, _l, _d) {
    _validate_col("dns4Aaddress")
    if (hdr()) {
        if (__PRIHDR__) print $dns4Aaddress, "Flows"
    } else if (!strisempty($dns4Aaddress) && !port(137)) { # ignore NBNS
        _l = split($dns4Aaddress, _d, ";");
        for (_i = 1; _i <= _l; _i++) {
            if (!strisempty(_d[_i]) && _d[_i] != "0.0.0.0") {
                aggr(_d[_i], "flows", n)
            }
        }
    }
}
