#!/usr/bin/env awk
#
# Returns true if 'v' is an IPv4 address in dotted-decimal notation
#
# Parameters:
#   - v : the value to test
#
# Dependencies:
#   - None
#
# Examples:
#   - tawk 'isipstr($dstIP4)' file.txt
#   - tawk 'isipstr($srcIP4) { print }' file.txt
#   - tawk '{ print isipstr("1.2.3.4") }' file.txt

@include "hdr"

function isipstr(v,        _i, _n, _ip) {
    if (hdr()) return __PRIHDR__
    _n = split(v, _ip, /\./)
    if (_n != 4) return 0;
    for (_i = 1; _i <= 4; _i++) {
        if (_ip[_i] !~ /^([01]?[0-9]?[0-9]|2[0-4][0-9]|25[0-5])$/) return 0
    }
    return 1
}
